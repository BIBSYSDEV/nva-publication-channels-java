plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco-report-aggregation'
    id 'com.diffplug.spotless'
}

group = 'no.sikt.nva'

dependencies {
    testImplementation libs.bundles.testing

    implementation libs.nva.s3
    implementation libs.aws.lambda.java.core
    implementation libs.bundles.nva
    implementation libs.bundles.logging
    implementation libs.bundles.jackson
    implementation libs.commons.validator
    implementation libs.open.csv
    implementation libs.aws.sdk2.s3
    implementation libs.aws.sdk2.dynamodb
    implementation libs.aws.sdk2.dynamodb.enhanced
    implementation libs.aws.appconfig
    implementation libs.aws.appconfigdata
    implementation libs.guava
    implementation libs.aws.sdk2.sdk.core
    testImplementation libs.dynamodDbLocal
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}

tasks.named('test') {
    useJUnitPlatform()
    failFast = true
    testLogging {
        events 'skipped', 'passed', 'failed'
    }
}

pmd {
    toolVersion = '7.20.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required
        html.required
        html.stylesheet = rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {

    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }
    }
}

spotless {
    java {
        toggleOffOn() // Ignores sections between `spotless:off` / `spotless:on`
        googleJavaFormat().reflowLongStrings().formatJavadoc(true).reorderImports(true)
    }

    format 'misc', {
        target '.gitignore', '.gitattributes', '.editorconfig', '**/*.gradle'
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}

// Reformat project before build and test if needed
tasks.named('build').configure {
    dependsOn 'spotlessApply'
}

tasks.named('test').configure {
    dependsOn 'spotlessApply'
}
